// Available to customize the build
plugins {
	id "de.undercouch.download" version "3.3.0"
}
apply plugin: 'java'

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

// download tasks
import de.undercouch.gradle.tasks.download.Download
import de.undercouch.gradle.tasks.download.Verify

task downloadBlesmolNetty(type: Download) {
    src 'https://github.com/Blesmol-IO/emf-launch/releases/download/0.1.0.201804041633/io.blesmol.launch.emf.site-0.1.0-SNAPSHOT.zip'
    dest "${bnd_cnf}/cache/p2-NettyModelView/io.blesmol.launch.emf.site-0.1.0.zip"
    onlyIfModified true
}

// might not be needed if bnd checks hashes
task verifyBlesmolNetty(type: Verify, dependsOn: downloadBlesmolNetty) {
    src new File("${bnd_cnf}/cache/p2-NettyModelView", 'io.blesmol.launch.emf.site-0.1.0.zip')
    algorithm 'MD5'
    // checksum cannot be a closure
    checksum '74159e73df867da8374118c8bc7c0ea1'
}

task unzipBlesmolNetty(dependsOn: verifyBlesmolNetty, type: Copy) {
    from zipTree(downloadBlesmolNetty.dest)
    into "${bnd_cnf}/cache/p2-NettyModelView"
	doLast {
	    bndWorkspace.repositories.findResult{ it.name == 'NettyModelView' ? it : null }.refresh()
	    println 'Refreshed NettyModelView'
	}
}

build.dependsOn unzipBlesmolNetty
